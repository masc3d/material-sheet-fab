// Gradle dependencies/plugins
buildscript {
    dependencies {
        // Kotlin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"

        // jooq codegen gradle plugin
        classpath 'com.github.ben-manes:gradle-jooq-plugin:+'

        // override jooq dependencies of gradle-jooq-plugin with desired version
        classpath("org.jooq:jooq-codegen:$v_jooq") { force true }
        classpath("org.jooq:jooq-meta:$v_jooq") { force true }

        // mysql jdbc connector
        classpath "mysql:mysql-connector-java:$v_mysql"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'jooq'

sourceCompatibility = 1.8
version = '0.1'
mainClassName = "org.deku.leo2.central.Main"

dependencies {
    // leo2 webservice dependencies
    compile project(':sx-common')
    compile project(':leo2-common')
    compile project(':leo2-node')

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-runtime:$v_kotlin"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin"

    // db
    compile "mysql:mysql-connector-java:$v_mysql"

    // jooq
    compile "org.jooq:jooq:$v_jooq"

    // test dependencies
    testCompile group: 'junit', name: 'junit', version: '4.+'

    // webservice client libraries for test cases
    testCompile "org.jboss.resteasy:resteasy-client:$v_resteasy"
    testCompile "org.springframework.boot:spring-boot-starter-test:$v_springboot"

    testCompile project(':sx-common')
}

run {
    errorOutput = System.err
    standardOutput = System.out
    standardInput = System.in
}

// Required for eclipse modelgen to find xml resources (eg. persistence.xml)
sourceSets.main.output.resourcesDir = sourceSets.main.output.classesDir
compileJava.dependsOn(processResources)

jar {
    manifest {
        attributes (
                'Main-Class': mainClassName,
                "Class-Path": configurations.compile.collect { it.getName() }.join(' ')
        )
    }
}


// Location for generated sources
// Directory for apt must be dedicated (no other generators may write here)
// otherwise triggering javac bug https://bugs.openjdk.java.net/browse/JDK-8062800
def srcdir_jooq = file("$projectDir/src/generated/jooq")

// Generate jooq for each compilation run
// tasks.compileJava.dependsOn(generateJooq)

jooq {
    jdbc {
        url 'jdbc:mysql://10.0.10.10:3306'
        driver 'com.mysql.jdbc.Driver'
        user 'root'
        password 'root'
    }
    generator {
        database {
            name 'org.jooq.util.mysql.MySQLDatabase'
            inputSchema 'dekuclient'

            includes 'tbldepotliste|mst_.*|sys_.*'
            //excludes '.*'

            // masc20140916. (almost) all tables
            //includes '.*'
            //excludes 'tbl_auftrag_info'
        }

        target {
            packageName 'org.deku.leo2.central.data.entities.jooq'
            directory srcdir_jooq.toString()
        }
    }
}
tasks.generateJooq.outputs.upToDateWhen { false }

// Packager task
task packagerDeploy(type: org.deku.gradle.PackagerDeployTask) {
    title = "LeoZ Central"
    mainJar = jar.archivePath
    jvmOptions = "-XX:+UseCompressedOops"
}
tasks.packagerDeploy.dependsOn(tasks.jar)

// masc20150507. task prototypes
////copying all dependencies attached to 'compile' into a specific folder
//task copyAllDependencies(type: Copy) {
//    //referring to the 'compile' configuration
//    from configurations.compile
//    into 'allLibs'
//}
//
//configurations.all.each {
//    println "compile: $it"
//}
//
//sourceSets.main.compileClasspath.each {
//    println "ss: $it"
//}

//configurations.compile.allDependencies.each {
//    println "$it.group:$it.name:$it.version"
//
//}
