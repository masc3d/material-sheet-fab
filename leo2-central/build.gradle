// Gradle dependencies/plugins
buildscript {
    dependencies {
        // spring boot
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$v_springboot")
        classpath("io.spring.gradle:dependency-management-plugin:$v_springboot_plugin")

        // cargo gradle plugin for remote delpoyment
        classpath 'org.gradle.api.plugins:gradle-cargo-plugin:1.+'

        // jooq codegen gradle plugin
        classpath 'com.github.ben-manes:gradle-jooq-plugin:+'

        // override jooq dependencies of gradle-jooq-plugin with desired version
        classpath("org.jooq:jooq-codegen:$v_jooq") {
            force: true
        }
        classpath("org.jooq:jooq-meta:$v_jooq") {
            force: true
        }

        // mysql jdbc connector
        classpath 'mysql:mysql-connector-java:5.+'
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "1.2.1"
}

apply plugin: 'war'
apply plugin: 'cargo'
apply plugin: 'application'
apply plugin: 'jooq'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'


sourceCompatibility = 1.8
version = '0.1'
mainClassName = "org.deku.leo2.central.Main"

repositories {
    mavenCentral()
}

dependencies {
    // leo2 webservice dependencies
    compile project(':leo2-common')
    compile project(':leo2-messaging')
    compile project(':leo2-node')
    compile project(':sx-common')

    // jooq
    compile "org.jooq:jooq:$v_jooq"

    // JSR-330
    // compile 'javax.inject:javax.inject:1'

    // databases
    compile 'mysql:mysql-connector-java:5.+'

    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat:$v_springboot")

    // test dependencies
    testCompile group: 'junit', name: 'junit', version: '4.+'

    // webservice client libraries for test cases
    testCompile "org.jboss.resteasy:resteasy-client:$v_resteasy"
    testCompile "org.springframework.boot:spring-boot-starter-test:$v_springboot"

    testCompile project(':sx-common')
}

run {
    errorOutput = System.err
    standardOutput = System.out
    standardInput = System.in
}

// war plugin configuration
war {
    // Some specific jetty components (and transitive deps) break tomcat deployment.
    // tomcat will fail with LifecycleException
    // In the long run we will probably need separate configurations/dependency sets for war/jar
    rootSpec.exclude("**/jetty*.jar")
    rootSpec.exclude("**/javax-websocket*.jar")
    rootSpec.exclude("**/javax.servlet-api*.jar")
}

// cargo plugin configuration
cargo {
    containerId = 'tomcat7x'
    port = 8080

    deployable {
        context = '/leo2'
    }

    remote {
        hostname = '10.0.10.10'
        username = 'tomcat'
        password = 'tomcat'
    }
}

// cargo task dependencies
tasks.cargoDeployRemote.dependsOn(war)
tasks.cargoRedeployRemote.dependsOn(war)

// Required for eclipse modelgen to find xml resources (eg. persistence.xml)
sourceSets.main.output.resourcesDir = sourceSets.main.output.classesDir
compileJava.dependsOn(processResources)

// Location for generated sources
// Directory for apt must be dedicated (no other generators may write here)
// otherwise triggering javac bug https://bugs.openjdk.java.net/browse/JDK-8062800
def srcdir_jooq = file("$projectDir/src/generated/jooq")

// Generate jooq for each compilation run
// tasks.compileJava.dependsOn(generateJooq)

jooq {
    jdbc {
        url 'jdbc:mysql://10.0.10.10:3306'
        driver 'com.mysql.jdbc.Driver'
        user 'root'
        password 'root'
    }
    generator {
        database {
            name 'org.jooq.util.mysql.MySQLDatabase'
            inputSchema 'dekuclient'

            includes 'tbldepotliste|country|holidayctrl|route|sector|station'
            //excludes '.*'

            // masc20140916. (almost) all tables
            //includes '.*'
            //excludes 'tbl_auftrag_info'
        }

        target {
            packageName 'org.deku.leo2.central.data.entities.jooq'
            directory srcdir_jooq.toString()
        }
    }
}
tasks.generateJooq.outputs.upToDateWhen { false }

task copyWebapp(type: Copy) {
    from 'src/main/webapp'
    into 'build/classes/main/webapp'
}

compileJava.dependsOn(copyWebapp)

// masc20150507. task prototypes
////copying all dependencies attached to 'compile' into a specific folder
//task copyAllDependencies(type: Copy) {
//    //referring to the 'compile' configuration
//    from configurations.compile
//    into 'allLibs'
//}
//
//configurations.all.each {
//    println "compile: $it"
//}
//
//sourceSets.main.compileClasspath.each {
//    println "ss: $it"
//}

//configurations.compile.allDependencies.each {
//    println "$it.group:$it.name:$it.version"
//
//}
