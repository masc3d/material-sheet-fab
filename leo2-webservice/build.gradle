// gradle dependencies/plugins
buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        // cargo gradle plugin for remote delpoyment
        classpath 'org.gradle.api.plugins:gradle-cargo-plugin:1.5'

        // jooq codegen gradle plugin
        classpath 'com.github.ben-manes:gradle-jooq-plugin:0.6'
        // override jooq dependencies of gradle-jooq-plugin with desired version
        classpath('org.jooq:jooq-codegen:3.4.1') {
            force: true
        }
        classpath('org.jooq:jooq-meta:3.4.1') {
            force: true
        }

        // mysql jdbc connector
        classpath 'mysql:mysql-connector-java:5.1.31'
    }
}

// plugins
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'war'
apply plugin: 'cargo'
apply plugin: 'jooq'

sourceCompatibility = 1.8
version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'

    // webservice dependencies
    compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.11'
    //TODO: masc20140722. jackson 2.x not working yet, probably auto service discovery, has to be investigated
    //compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.11'
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson1:2.11'

    // mysql jdbc connector
    compile 'mysql:mysql-connector-java:5.1.31'

    // jooq
    compile 'org.jooq:jooq:3.4.1'
    compile 'org.jooq:jooq-meta:3.4.1'
    compile 'org.jooq:jooq-codegen:3.4.1'

    compile 'org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.11'

    // leo2 webservice dependencies
    compile project(':leo2-common')
}

jooq {
    jdbc {
        url 'jdbc:mysql://10.0.10.10:3306'
        driver 'com.mysql.jdbc.Driver'
        user 'root'
        password 'root'
    }
    generator {
        database {
            name 'org.jooq.util.mysql.MySQLDatabase'
            inputSchema 'dekuclient'
            includes '.*'
            excludes 'tbl_auftrag_info'
        }

        target {
            packageName 'org.leo2.jooq.dekuclient'
            directory 'src/main/java'
        }
    }
}

// war plugin configuration
war {
}

// cargo plugin configuration
cargo {
    containerId = 'tomcat7x'
    port = 8080

    deployable {
        context = '/leo2'
    }

    remote {
        hostname = '10.0.10.10'
        username = 'tomcat'
        password = 'tomcat'
    }
}

// task dependencies
tasks.cargoDeployRemote.dependsOn(war)
tasks.cargoRedeployRemote.dependsOn(war)
tasks.generateJooq.outputs.upToDateWhen { false }
