buildscript {
    repositories {
        jcenter()
    }
    ext {
        v_android_buildtools = '26.0.0'
        v_android_support = '25.4.0'
        v_anko = '0.10.1'
        v_javamail = '1.6.0'
        v_junit = '4.12'
        v_kotlin = '1.1.3-2'
        v_ormlite = '5.0'
        v_paho = '1.1.1'
        v_roboelectric = '3.3.2'
        v_rxlifecycle = '2.1.0'
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }
}
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

android {
    publishNonDefault true
    compileSdkVersion 26
    buildToolsVersion v_android_buildtools
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
    }

    dataBinding {
        enabled true
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            // Enable pre-dexing to produce an APK that can be tested on
            // Android 5.0+ without the time-consuming DEX build processes.
            minSdkVersion 21
        }

        prod {
            // The actual minSdkVersion for the production version.
            minSdkVersion 16
        }
    }
}

dependencies {
    compile(
            project(":sx-common"),

            fileTree(dir: 'libs', include: ['*.jar']),

            "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin",

            // Anko
            "org.jetbrains.anko:anko-sqlite:$v_anko",

            // Android support libs
            "com.android.support:support-annotations:$v_android_support",
            "com.android.support:appcompat-v7:$v_android_support",
            "com.android.support:support-v4:$v_android_support",
            "com.android.support:design:$v_android_support",

            // rx
            "io.reactivex.rxjava2:rxjava:2.1.2",
            "io.reactivex.rxjava2:rxandroid:2.0.1",

            // rxlifecycle
            "com.trello.rxlifecycle2:rxlifecycle:$v_rxlifecycle",
            "com.trello.rxlifecycle2:rxlifecycle-kotlin:$v_rxlifecycle",
            "com.trello.rxlifecycle2:rxlifecycle-components:$v_rxlifecycle",
            "com.trello.rxlifecycle2:rxlifecycle-android:$v_rxlifecycle",
            "com.jakewharton.rxbinding2:rxbinding:2.0.0",

            // JavaMail
            "com.sun.mail:android-mail:$v_javamail",
            "com.sun.mail:android-activation:$v_javamail",
    )

    provided(
            project(':sx-mq'),

            'com.journeyapps:zxing-android-embedded:3.5.0',

            // MQTT persistence
            "org.eclipse.paho:org.eclipse.paho.client.mqttv3:$v_paho",
            "org.eclipse.paho:org.eclipse.paho.android.service:$v_paho",

            // ReactiveNetwork
            "com.github.pwittchen:reactivenetwork-rx2:0.10.0",

            // Flexible adapter
            "eu.davidea:flexible-adapter:5.0.0-rc2",
            "com.eightbitlab:blurview:1.3.3"
    )

    testCompile(
            "junit:junit:$v_junit",
            "org.robolectric:robolectric:$v_roboelectric",
            "org.robolectric:shadows-multidex:$v_roboelectric"
    )

    androidTestCompile(
            "junit:junit:$v_junit",

            "com.android.support:support-annotations:$v_android_support",
            "com.android.support.test:runner:0.5",
            "com.android.support.test:rules:0.5",

            // Logging
            dependencies.create("com.github.tony19:logback-android-classic:1.1.1-6") {
                exclude group: 'com.google.android', module: 'android'
            },

            "org.eclipse.paho:org.eclipse.paho.client.mqttv3:$v_paho"
    )
}
