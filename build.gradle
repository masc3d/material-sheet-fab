group 'sx'
version '1.0'

buildscript {
    ext {
        v_activemq = '5.14.1'
        v_artemis = '1.4.0'
        v_kotlin = '1.0.4'
        v_protostuff = '1.5.0'
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }

    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'project-report'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

configurations {
    provided {
        dependencies.all { dep ->
            configurations.default.exclude group: dep.group, module: dep.name
        }
    }
    compile.extendsFrom provided
}

// Prototype tests

sourceSets {
    test {
        java {
            srcDirs += file("$projectDir/src/prototype/java")
        }
    }
}

test {
    exclude 'sx/jms/prototype/**'
}

dependencies {
    compile(
            project(':libs:sx-common'),
            project(':libs:sx-serialization'),
            // Kotlin
            "org.jetbrains.kotlin:kotlin-runtime:$v_kotlin",
            "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin",
            // Guava
            "com.google.guava:guava:20.0",
            // JMS
            "javax.jms:jms-api:1.1-rev-1"
    )

    provided(
            // Spring
            "org.springframework:spring-jms:4.3.3.RELEASE",
            // ActiveMQ
            "org.apache.activemq:activemq-broker:$v_activemq",
            "org.apache.activemq:activemq-kahadb-store:$v_activemq",
            "org.apache.activemq:activemq-jms-pool:$v_activemq",
            "org.apache.activemq:activemq-jaas:$v_activemq",
            "org.apache.activemq:activemq-mqtt:$v_activemq",
            dependencies.create("org.apache.activemq:activemq-http:$v_activemq") {
                exclude group: 'org.springframework'
                exclude group: 'org.apache.activemq', module: 'activemq-spring'
                exclude group: 'org.eclipse.jetty.aggregate'
            },
            // For ActiveMQ tunnel servlet
            "javax.servlet:javax.servlet-api:3.1.0",

            // Artemis
            "org.apache.activemq:artemis-jms-server:$v_artemis",
            "org.apache.activemq:artemis-amqp-protocol:$v_artemis",
            "org.apache.activemq:artemis-openwire-protocol:$v_artemis"
    )

    testCompile(
            "junit:junit:4.12",
            "ch.qos.logback:logback-classic:1.1.7"
    )
}
