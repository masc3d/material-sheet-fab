group = 'sx'
version = '0.1'

buildscript {
    ext {
        v_feign = '9.5.1'
        v_jackson = '2.9.0'
        v_javax_persistence = '2.1.1'
        v_jersey = '2.26'
        v_jgit = '4.8.0.201706111038-r'
        v_jsch = '0.0.9'
        v_querydsl = '4.1.4'
        v_kotlin = '1.1.4-3'
        v_requery = '1.4.0'
        v_resteasy = '3.1.4.Final'
        v_slf4j = '1.7.25'
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }

    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'project-report'

// masc20150818., Keep source compatibility 1.7 for android support
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenCentral()
}

dependencies {
    compile(
            // Kotlin
            "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin",
            // Logging
            "org.slf4j:slf4j-api:$v_slf4j",
            "org.slf4j:jcl-over-slf4j:$v_slf4j",
            // RX
            "io.reactivex.rxjava2:rxjava:2.1.3",
            "io.reactivex:rxkotlin:2.0.0-RC1",
    )

    compileOnly(
            // Misc
            "org.apache.commons:commons-lang3:3.6",
            // JAX-RS
            "javax.ws.rs:javax.ws.rs-api:2.0.1",
            // JNA
            "net.java.dev.jna:jna:4.4.0",
            "net.java.dev.jna:jna-platform:4.4.0",

            // REST
            // Jersey REST client
            "org.glassfish.jersey.core:jersey-client:$v_jersey",
            "org.glassfish.jersey.ext:jersey-proxy-client:$v_jersey",
            // RESTeasy client
            "org.jboss.resteasy:resteasy-client:$v_resteasy",
            // Feign REST
            "io.github.openfeign:feign-jackson:$v_feign",
            dependencies.create("io.github.openfeign:feign-jaxrs:$v_feign") {
                exclude group: "javax.ws.rs", module: "jsr311-api"
            },
            // YAML
            "org.yaml:snakeyaml:1.18",
            // Jackson
            "com.fasterxml.jackson.core:jackson-databind:$v_jackson",
            "com.fasterxml.jackson.core:jackson-annotations:$v_jackson",

            // JGit
            "org.eclipse.jgit:org.eclipse.jgit:$v_jgit",
            // For authenticating ssh/jgit against key agent
            "com.jcraft:jsch.agentproxy.connector-factory:$v_jsch",
            "com.jcraft:jsch.agentproxy.jsch:$v_jsch",
            "com.jcraft:jsch.agentproxy.sshagent:$v_jsch",
            "com.jcraft:jsch.agentproxy.pageant:$v_jsch",

            // QueryDSL
            "com.querydsl:querydsl-core:$v_querydsl",
            "com.querydsl:querydsl-jpa:$v_querydsl",

            // JPA
            "org.eclipse.persistence:javax.persistence:$v_javax_persistence",

            // Requery
            "io.requery:requery:$v_requery",
            "io.requery:requery-kotlin:$v_requery",

            // JUnit
            "junit:junit:4.12",
    )

    testCompile(
            "junit:junit:4.12",
            "ch.qos.logback:logback-classic:1.2.3"
    )
}

//region Test artifact configuration
// For sharing test classes (eg. for running as android instrumentation in android projects)
configurations {
    testArtifacts
}

task testJar(type: Jar) {
    classifier "test"
    from sourceSets.test.output
}

artifacts {
    testArtifacts testJar
}
//endregion
