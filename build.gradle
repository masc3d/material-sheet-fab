group 'sx'
version '0.1'

buildscript {
    ext {
        v_kotlin = '1.0.6'
    }

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }
}

apply plugin: 'kotlin'

repositories {
    mavenCentral()
}

//region Prototype test setup
configurations {
    prototypeCompile.extendsFrom testCompile
    prototypeRuntime.extendsFrom testRuntime
}

sourceSets {
    prototype {
        java.srcDirs = ['src/prototype/java']
        resources.srcDirs = ['src/prototype/resources']
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

task prototype(type: Test, description: 'Run prototype tests.') {
    testClassesDir sourceSets.prototype.output.classesDir
    classpath = sourceSets.prototype.runtimeClasspath
}
//endregion

dependencies {
    compile(
            project(':sx-common'),
            project(':sx-serialization'),
            // Kotlin
            "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin",
            // Kryo
            "com.esotericsoftware:kryo:4.0.0"
    )

    testCompile(
            "junit:junit:4.12",
            "ch.qos.logback:logback-classic:1.1.9"
    )
}
