import org.flywaydb.gradle.FlywayExtension
import org.flywaydb.gradle.task.FlywayMigrateTask
import org.flywaydb.gradle.task.FlywayCleanTask
import org.flywaydb.gradle.task.FlywayInfoTask
import org.flywaydb.gradle.task.FlywayBaselineTask
import org.flywaydb.gradle.task.FlywayRepairTask

group 'org.deku.leoz'
version '1.0-SNAPSHOT'

buildscript {
    ext {
        v_flyway = '4.1.2'
        v_mysql = '5.1.40'

        DB_MYSQL_JDBC_DRIVER = 'com.mysql.jdbc.Driver'
        FLYWAY_MIGRATION_DIR = new File(project.projectDir, 'sql')
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        // mysql jdbc connector
        classpath "mysql:mysql-connector-java:$v_mysql"
        // Flyway
        classpath "org.flywaydb:flyway-gradle-plugin:$v_flyway"
    }
}

apply plugin: 'org.flywaydb.flyway'


// Remove standard flyway tasks
tasks.remove(tasks.flywayBaseline)
tasks.remove(tasks.flywayClean)
tasks.remove(tasks.flywayInfo)
tasks.remove(tasks.flywayMigrate)
tasks.remove(tasks.flywayRepair)
tasks.remove(tasks.flywayValidate)

/**
 * Common flyway extension/parameters
 * @return
 */
 def createFlywayExtension() {
    def extension = new FlywayExtension()
    extension.driver = ext.DB_MYSQL_JDBC_DRIVER
    extension.user = 'root'
    extension.password = 'dekudb'
    extension.schemas = ['dekuclient']
    extension.locations = ["filesystem:${ext.FLYWAY_MIGRATION_DIR}"]
    return extension
}

//
// Leoz dev database tasks
//

def createFlywayDevExtension() {
    def extension = this.createFlywayExtension()
    extension.user = 'root'
    extension.password = 'root'
    extension.url = 'jdbc:mysql://10.0.10.10:3306?useSSL=false'
    return extension
}

task flywayDevInfo(type: FlywayInfoTask) {
    extension = createFlywayDevExtension()
}

task flywayDevClean(type: FlywayCleanTask) {
    extension = createFlywayDevExtension()
}

task flywayDevRepair(type: FlywayRepairTask) {
    extension = createFlywayDevExtension()
}

task flywayDevMigrate(type: FlywayMigrateTask) {
    extension = createFlywayDevExtension()
    extension.target = 6
}

//
// Leoz integration (leox) database tasks
//

def createFlywayLeoxExtension() {
	def extension = this.createFlywayExtension()
	extension.url = 'jdbc:mysql://leox.derkurier.de:3320?useSSL=false'
    extension.user = 'root'
    extension.password = 'dekudb'
    return extension
}

task flywayLeoxInfo(type: FlywayInfoTask) {
    extension = createFlywayLeoxExtension()
}

task flywayLeoxRepair(type: FlywayRepairTask) {
    extension = createFlywayLeoxExtension()
}

task flywayLeoxMigrate(type: FlywayMigrateTask) {
    extension = createFlywayLeoxExtension()
    extension.target = 5
}

task flywayLeoxBaseline(type: FlywayBaselineTask) {
    extension = createFlywayLeoxExtension()
    extension.baselineVersion = 1
}
