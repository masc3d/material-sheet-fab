buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
    }

    ext {
        v_android_gradle = '3.0.1'
        v_android_buildtools = '27.0.3'
        v_android_support = '27.1.0'
        v_android_test = '1.0.1'
        v_android_gms = '11.8.0'
        v_anko = '0.10.4'
        v_javamail = '1.6.0'
        v_junit = '4.12'
        v_kotlin = '1.2.30'
        v_material_dialogs = '0.9.6.0'
        v_ormlite = '5.0'
        v_paho = '1.2.0'
        v_roboelectric = '3.7.1'
        v_rxlifecycle = '2.2.1'
        v_truetime = '3.3'
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        // masc20181101. android support
        // API levels < 24 are not compatible with some java8 bytecode kt compiler produces
        jvmTarget = JavaVersion.VERSION_1_6
    }
}

repositories {
    mavenCentral()
    jcenter()
    google()
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 27
    buildToolsVersion v_android_buildtools

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled true
    }

    lintOptions {
        abortOnError true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-debug.pro'
        }
    }
}

dependencies {
    implementation(
            project(":sx-common"),

            fileTree(dir: 'libs', include: ['*.jar']),

            "com.jakewharton.threetenabp:threetenabp:1.0.5",

            "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin",

            // Anko
            "org.jetbrains.anko:anko-sqlite:$v_anko",

            // Android support libs
            "com.android.support:support-annotations:$v_android_support",
            "com.android.support:appcompat-v7:$v_android_support",
            "com.android.support:support-v4:$v_android_support",
            "com.android.support:design:$v_android_support",

            // rx
            "io.reactivex.rxjava2:rxjava:2.1.11",
            "io.reactivex.rxjava2:rxandroid:2.0.2",
            "io.reactivex:rxkotlin:2.0.0-RC1",

            // rxlifecycle
            "com.trello.rxlifecycle2:rxlifecycle:$v_rxlifecycle",
            "com.trello.rxlifecycle2:rxlifecycle-kotlin:$v_rxlifecycle",
            "com.trello.rxlifecycle2:rxlifecycle-components:$v_rxlifecycle",
            "com.trello.rxlifecycle2:rxlifecycle-android:$v_rxlifecycle",
            "com.jakewharton.rxbinding2:rxbinding:2.1.1",

            // Logging
            "org.slf4j:slf4j-api:$v_slf4j",
            "org.slf4j:jcl-over-slf4j:$v_slf4j",

            // JavaMail
            "com.sun.mail:android-mail:$v_javamail",
            "com.sun.mail:android-activation:$v_javamail"
    )

    compileOnly(
            project(':sx-mq'),

            "com.journeyapps:zxing-android-embedded:3.6.0",

            // MQTT persistence
            "org.eclipse.paho:org.eclipse.paho.client.mqttv3:$v_paho",

            // Dialog
            "com.afollestad.material-dialogs:core:$v_material_dialogs",
            "com.afollestad.material-dialogs:commons:$v_material_dialogs",

            // ReactiveNetwork
            "com.github.pwittchen:reactivenetwork-rx2:0.12.3",

            // Flexible adapter
            "eu.davidea:flexible-adapter:5.0.1",
            "com.eightbitlab:blurview:1.3.4",

            // Requery sqlite
            "io.requery:sqlite-android:3.22.0",

            // GMS
            "com.google.android.gms:play-services-location:$v_android_gms",

            // TrueTime
            "com.github.instacart.truetime-android:library:$v_truetime",
            "com.github.instacart.truetime-android:library-extension-rx:$v_truetime"
    )

    testImplementation(
            project(':sx-mq'),

            "junit:junit:$v_junit",
            "org.robolectric:robolectric:$v_roboelectric",
            "org.robolectric:shadows-multidex:$v_roboelectric",

            // MQTT client
            "org.eclipse.paho:org.eclipse.paho.client.mqttv3:$v_paho"
    )

    androidTestImplementation(
            "junit:junit:$v_junit",

            "com.android.support:support-annotations:$v_android_support",
            "com.android.support.test:runner:$v_android_test",
            "com.android.support.test:rules:$v_android_test",

            // Logging
            dependencies.create("com.github.tony19:logback-android-classic:1.1.1-6") {
                exclude group: 'com.google.android', module: 'android'
            },

            "org.eclipse.paho:org.eclipse.paho.client.mqttv3:$v_paho"
    )

    kapt(
            "com.android.databinding:compiler:$v_android_gradle"
    )
}
