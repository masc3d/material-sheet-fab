buildscript {
    ext {
        v_activemq = '5.13.3'
        v_kotlin = '1.0.2-1'
        v_logback = '1.1.7'
        v_snappy = "1.1.2.4"
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }

    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'

// masc20150818., Keep source compatibility 1.7 for android support
sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '1.0'

repositories {
    mavenCentral()
}

configurations {
    provided {
        dependencies.all { dep ->
            configurations.default.exclude group: dep.group, module: dep.name
        }
    }
    compile.extendsFrom provided
}

dependencies {
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-runtime:$v_kotlin"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin"

    // Logging
    compile "commons-logging:commons-logging:1.2"
    compile "org.slf4j:jcl-over-slf4j:1.7.21"
    provided "ch.qos.logback:logback-classic:$v_logback"
    testCompile "ch.qos.logback:logback-classic:$v_logback"

    // Misc
    compile "com.google.guava:guava:18.0"
    compile "org.apache.commons:commons-lang3:3.4"

    // RX
    compile "io.reactivex:rxjava:1.1.6"
    compile 'io.reactivex:rxkotlin:0.60.0'

    // JMS
    provided "javax.jms:jms-api:1.1-rev-1"

    // JAX-RS
    provided "javax.ws.rs:javax.ws.rs-api:2.0.1"

    // Ini/conf file support
    compile 'org.ini4j:ini4j:0.5.4'

    provided "javax.servlet:javax.servlet-api:3.1.0"

    // ActiveMQ
    provided "org.apache.activemq:activemq-broker:$v_activemq"
    provided "org.apache.activemq:activemq-kahadb-store:$v_activemq"
    provided "org.apache.activemq:activemq-leveldb-store:$v_activemq"
    provided "org.apache.activemq:activemq-jms-pool:$v_activemq"
    provided "org.apache.activemq:activemq-jaas:$v_activemq"
    provided "org.apache.activemq:activemq-mqtt:$v_activemq"
    provided("org.apache.activemq:activemq-http:$v_activemq") {
        exclude group: 'org.springframework'
        exclude group: 'org.apache.activemq', module: 'activemq-spring'
        exclude group: 'org.eclipse.jetty.aggregate'
    }

    // Mina/SSHD
    provided "org.apache.sshd:sshd-core:1.2.0"

    // Spring
    provided "org.springframework:spring-jms:4.2.6.RELEASE"

    // Kryo
    provided "com.esotericsoftware:kryo:3.0.3"

    // Snappy
    provided "org.xerial.snappy:snappy-java:$v_snappy"

    // JNA
    provided "net.java.dev.jna:jna:4.2.2"
    provided "net.java.dev.jna:jna-platform:4.2.2"

    provided "junit:junit:4.12"
    testCompile "junit:junit:4.12"
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()

        force "com.fasterxml.jackson.core:jackson-annotations:2.7.4"
        force "com.google.guava:guava:19.0"
        force "commons-logging:commons-logging:1.2"
        force "io.reactivex:rxjava:1.1.6"
        force "org.fusesource.hawtdispatch:hawtdispatch-transport:1.22"
        force "org.fusesource.hawtbuf:hawtbuf:1.11"
        force "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin"
        force "org.slf4j:slf4j-api:1.7.21"
        force "org.xerial.snappy:snappy-java:$v_snappy"
    }
}