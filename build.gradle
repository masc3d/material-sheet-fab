import org.flywaydb.gradle.FlywayExtension
import org.flywaydb.gradle.task.FlywayMigrateTask
import org.flywaydb.gradle.task.FlywayInfoTask

group 'org.deku.leoz'
version '1.0-SNAPSHOT'

buildscript {
    ext {
        v_flyway = '4.0.3'
        v_mysql = '5.1.39'

        DB_MYSQL_JDBC_DRIVER = 'com.mysql.jdbc.Driver'
        FLYWAY_MIGRATION_DIR = new File(project.projectDir, 'sql')
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        // mysql jdbc connector
        classpath "mysql:mysql-connector-java:$v_mysql"
        // Flyway
        classpath "org.flywaydb:flyway-gradle-plugin:$v_flyway"
    }
}

apply plugin: 'org.flywaydb.flyway'


// Remove standard flyway tasks
tasks.remove(tasks.flywayBaseline)
tasks.remove(tasks.flywayClean)
tasks.remove(tasks.flywayInfo)
tasks.remove(tasks.flywayMigrate)
tasks.remove(tasks.flywayRepair)
tasks.remove(tasks.flywayValidate)

/**
 * Common flyway extension/parameters
 * @return
 */
def createFlywayExtension() {
    def extension = new FlywayExtension()
    extension.driver = ext.DB_MYSQL_JDBC_DRIVER
    extension.user = 'root'
    extension.password = 'root'
    extension.schemas = ['dekuclient']
    extension.locations = ["filesystem:${ext.FLYWAY_MIGRATION_DIR}"]
    return extension
}

// LEO integration database tasks

def DB_DEV_JDBC_URL = 'jdbc:mysql://10.0.10.10:3306?useSSL=false'
def DB_LEOX_JDBC_URL = 'jdbc:mysql://leox.derkurier.de:3306?useSSL=false'

task flywayDevMigrate(type: FlywayMigrateTask) {
    extension = createFlywayExtension()

    extension.url = DB_DEV_JDBC_URL
    extension.target = 4
}

task flywayDevInfo(type: FlywayInfoTask) {
    extension = createFlywayExtension()

    extension.url = DB_DEV_JDBC_URL
}

task flywayLeoxMigrate(type: FlywayMigrateTask) {
    extension = createFlywayExtension()

    extension.url = DB_LEOX_JDBC_URL
    extension.target = 4
}

task flywayLeoxInfo(type: FlywayInfoTask) {
    extension = createFlywayExtension()

    extension.url = DB_LEOX_JDBC_URL
}
