group = 'sx'
version = '0.1'

buildscript {
    ext {
        v_kotlin = '1.0.6'
        v_slf4j = '1.7.23'
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }

    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'project-report'

// masc20150818., Keep source compatibility 1.7 for android support
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

configurations {
    // TODO: compileOnly doesn't work property with AndroidStudio (will compile, but doesn't see classes in editor etc.)
    provided {
        dependencies.all { dep ->
            configurations.default.exclude group: dep.group, module: dep.name
        }
    }
    compile.extendsFrom provided
}

dependencies {
    compile(
            // Kotlin
            "org.jetbrains.kotlin:kotlin-runtime:$v_kotlin",
            "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin",
            // Logging
            "org.slf4j:slf4j-api:$v_slf4j",
            "org.slf4j:jcl-over-slf4j:$v_slf4j",
            // RX
            "io.reactivex:rxjava:1.2.6",
            "io.reactivex:rxjava-async-util:0.21.0",
            "io.reactivex:rxkotlin:0.60.0",
    )

    provided(
            // Misc
            "org.apache.commons:commons-lang3:3.5",
            // JAX-RS
            "javax.ws.rs:javax.ws.rs-api:2.0.1",
            // JNA
            "net.java.dev.jna:jna:4.3.0",
            "net.java.dev.jna:jna-platform:4.3.0",
            // Feign REST
            "com.netflix.feign:feign-core:8.18.0",
            // JUnit
            "junit:junit:4.12"
    )

    testCompile(
            "junit:junit:4.12",
            "ch.qos.logback:logback-classic:1.2.1"
    )
}