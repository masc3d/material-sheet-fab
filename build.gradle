buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
    }

    ext {
        v_android_buildtools = '25.0.2'
        v_android_support = '25.3.1'
        v_anko = '0.10.0'
        v_javamail = '1.5.5'
        v_junit = '4.12'
        v_kotlin = '1.1.2-4'
        v_ormlite = '5.0'
        v_paho = '1.1.1'
        v_roboelectric = '3.3.2'
        v_rxlifecycle = '2.1.0'
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-alpha3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }
}
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 25
    buildToolsVersion v_android_buildtools
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
    }
}

dependencies {
    implementation(
            project(":sx-common"),

            fileTree(dir: 'libs', include: ['*.jar']),

            "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin",
            dependencies.create("org.jetbrains.anko:anko:$v_anko") { details ->
                // masc20170516. too early for those
                exclude group: 'org.jetbrains.anko', module: 'anko-sdk25'
                exclude group: 'org.jetbrains.anko', module: 'anko-coroutines'
                exclude group: 'org.jetbrains.anko', module: 'anko-sdk25-coroutines'
                exclude group: 'org.jetbrains.anko', module: 'anko-appcompat-v7-coroutines'
            },

            // Android support libs
            "com.android.support:support-annotations:$v_android_support",
            "com.android.support:appcompat-v7:$v_android_support",
            "com.android.support:support-v4:$v_android_support",
            "com.android.support:design:$v_android_support",

            // rx
            "io.reactivex.rxjava2:rxjava:2.1.0",
            "io.reactivex.rxjava2:rxandroid:2.0.1",

            // rxlifecycle
            "com.trello.rxlifecycle2:rxlifecycle:$v_rxlifecycle",
            "com.trello.rxlifecycle2:rxlifecycle-kotlin:$v_rxlifecycle",
            "com.trello.rxlifecycle2:rxlifecycle-components:$v_rxlifecycle",
            "com.trello.rxlifecycle2:rxlifecycle-android:$v_rxlifecycle",
            "com.jakewharton.rxbinding2:rxbinding:2.0.0",

            // JavaMail
            "com.sun.mail:android-mail:$v_javamail",
            "com.sun.mail:android-activation:$v_javamail",
    )

    compileOnly(
            project(':sx-mq'),

            'com.journeyapps:zxing-android-embedded:3.5.0',

            // MQTT persistence
            "org.eclipse.paho:org.eclipse.paho.client.mqttv3:$v_paho",
            "org.eclipse.paho:org.eclipse.paho.android.service:$v_paho",

            // ReactiveNetwork
            'com.github.pwittchen:reactivenetwork-rx2:0.9.1'
    )

    testImplementation(
            "junit:junit:$v_junit",
            "org.robolectric:robolectric:$v_roboelectric",
            "org.robolectric:shadows-multidex:$v_roboelectric"
    )

    androidTestImplementation(
            "junit:junit:$v_junit",

            "com.android.support:support-annotations:$v_android_support",
            "com.android.support.test:runner:0.5",
            "com.android.support.test:rules:0.5",

            // Logging
            dependencies.create("com.github.tony19:logback-android-classic:1.1.1-6") {
                exclude group: 'com.google.android', module: 'android'
            },

            "org.eclipse.paho:org.eclipse.paho.client.mqttv3:$v_paho"
    )
}
