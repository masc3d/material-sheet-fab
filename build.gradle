apply plugin: 'idea'
apply plugin: 'eclipse'

version = '0.1'

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'project-report'

    // Reporting setup
    tasks.compileJava.dependsOn(tasks.dependencyReport)
    tasks.compileJava.dependsOn(tasks.htmlDependencyReport)
    projectReportDirName = '../../reports'

    // Global repositories
    repositories {
        jcenter()
        maven {
            url "http://repo.spring.io/snapshot"
        }
        mavenCentral()
    }

    // Global variables
    ext {
        v_activemq = "5.+"
        v_eclipselink = "2.+"
        v_guava = "18.0"
        v_h2 = "1.+"
        v_hk2 = "2.+"
        v_jackson = "2.+"
        v_javax_servlet = "3.1.0"
        v_jersey = "2.+"
        v_jetty = "9.2.+"
        v_jooq = "3.+"
        v_junit = "4.+"
        v_querydsl = "3.+"
        // Resteasy > 3.0.9 breaks integration with spring-boot.
        // it actually still works fine when spring is initialized via resteasy's SpringContextLoaderListener
        v_resteasy = "3.0.+"
        v_spring = "4.1.6.+"
        v_spring_platform = "1.1.3.BUILD-SNAPSHOT"
        v_springboot = "1.2.4.+"
        v_springboot_dep_plugin = "+"
        v_springboot_gradle_plugin = "1.3.0.+"
    }

    // Setting properties for spring-boot depdendency management
    ext["spring.version"] = v_spring
    ext["jackson.version"] = v_jackson
    ext["activemq.version"] = v_activemq
    ext["guava.version"] = v_guava

    // Global buildscript repositories
    buildscript {
        repositories {
            jcenter()
            maven {
                url "http://repo.spring.io/snapshot"
            }
            mavenCentral()
        }
    }

    // Gloal dependency resolution
    configurations.all {
        resolutionStrategy {
            // fail eagerly on version conflict (includes transitive dependencies)
            // e.g. multiple different versions of the same dependency (group and name are equal)
            failOnVersionConflict()

            // Force overrides for newer versions of (transitive) dependencies for all projects consistently

//            componentSelection { m ->
//                all { ComponentSelection selection ->
//                    if (selection.candidate.group == 'org.springframework.boot')
//                        println selection.candidate.module
//                    }
//            }

            // Version overrides
            resolutionStrategy.eachDependency { DependencyResolveDetails details ->
                // Spring
                if (details.requested.group == 'org.springframework') {
                    details.useVersion v_spring
                // Jackson
                } else if (details.requested.group.startsWith('com.fasterxml.jackson')) {
                    details.useVersion v_jackson
                }
            }

            // Java
            force "javax.servlet:javax.servlet-api:$v_javax_servlet"

            // Glassfish
            force "org.glassfish.hk2:hk2:$v_hk2"
            force "org.glassfish.hk2:hk2-api:$v_hk2"
            force "org.glassfish.hk2:hk2-locator:$v_hk2"
            force "org.glassfish.hk2:spring-bridge:$v_hk2"
            force "org.glassfish.hk2.external:javax.inject:$v_hk2"
            force "org.glassfish:javax.el:3.+"

            // Jersey
            force "org.glassfish.jersey.containers:jersey-container-servlet-core:$v_jersey"
            force "org.glassfish.jersey.core:jersey-server:$v_jersey"

            // Jetty
            force "org.eclipse.jetty.aggregate:jetty-all:$v_jetty"
            force "org.eclipse.jetty.websocket:javax-websocket-server-impl:$v_jetty"
            force "org.eclipse.jetty.websocket:websocket-server:$v_jetty"
            force "org.eclipse.jetty:jetty-webapp:$v_jetty"

            // Jooq
            force "org.jooq:jooq-meta:$v_jooq"
            force "org.jooq:jooq-codegen:$v_jooq"

            // Apache common
            force "commons-logging:commons-logging:1.+"
            force "org.apache.httpcomponents:httpclient:4.+"
            force "org.apache.ant:ant:1.+"
            force "org.apache.ant:ant-launcher:1.+"

            // Hawt
            force "org.fusesource.hawtdispatch:hawtdispatch-transport:1.+"
            force "org.fusesource.hawtbuf:hawtbuf:1.+"

            force "com.google.guava:guava:$v_guava"
            force "com.mysema.querydsl:querydsl-codegen:$v_querydsl"
            force "org.javassist:javassist:3.+"
            force "com.thoughtworks.xstream:xstream:1.+"
            force "xml-apis:xml-apis:1.+"
            force "org.hamcrest:hamcrest-core:1.+"
            force "org.yaml:snakeyaml:1.+"

            // SLF4J
            force "org.slf4j:slf4j-api:1.7.+"
            force "org.slf4j:log4j-over-slf4j:1.7.+"
            force "org.slf4j:jcl-over-slf4j:1.7.+"
            force "org.slf4j:jul-to-slf4j:1.7.+"

            force "org.jboss.logging:jboss-logging:3.+"
            force "org.jboss.logging:jboss-logging-annotations:1.+"
        }
    }
}
