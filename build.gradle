buildscript {
    repositories {
        jcenter()
    }
    ext {
        v_kotlin = '1.1.4-3'
        v_android_buildtools = '26.0.1'
        v_android_support = '26.0.1'
        v_android_test = '1.0.0'
        v_rxlifecycle = '2.1.0'
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }
}
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

configurations {
    // Configurations for libraries using dev/prod product flavors (for improved build performance)
    devDebugCompile
    devReleaseCompile
    prodDebugCompile
    prodReleaseCompile
}

android {
    publishNonDefault true
    compileSdkVersion 26
    buildToolsVersion v_android_buildtools
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            // Enable pre-dexing to produce an APK that can be tested on
            // Android 5.0+ without the time-consuming DEX build processes.
            minSdkVersion 21
        }

        prod {
            // The actual minSdkVersion for the production version.
            minSdkVersion 16
        }
    }
}

dependencies {
    devDebugCompile(
            project(path: ':sx-android', configuration: 'devDebug')
    )

    devReleaseCompile(
            project(path: ':sx-android', configuration: 'devRelease')
    )

    prodDebugCompile(
            project(path: ':sx-android', configuration: 'prodDebug')
    )

    prodReleaseCompile(
            project(path: ':sx-android', configuration: 'prodRelease')
    )

    compile(
            fileTree(dir: 'libs', include: ['*.jar']),

            "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin",

            // Android support libs
            "com.android.support:support-annotations:$v_android_support",
            "com.android.support:appcompat-v7:$v_android_support",
            "com.android.support:support-v4:$v_android_support",
            "com.android.support:design:$v_android_support",

            // rx
            "io.reactivex.rxjava2:rxjava:2.1.3",
            "io.reactivex.rxjava2:rxandroid:2.0.1",

            // rxlifecycle
            "com.trello.rxlifecycle2:rxlifecycle:$v_rxlifecycle",
            "com.trello.rxlifecycle2:rxlifecycle-kotlin:$v_rxlifecycle",
            "com.trello.rxlifecycle2:rxlifecycle-components:$v_rxlifecycle"
    )

    androidTestCompile(
            "junit:junit:4.12",
            "com.android.support:support-annotations:$v_android_support",
            "com.android.support.test:runner:$v_android_test",
            "com.android.support.test:rules:$v_android_test"
    )
}
