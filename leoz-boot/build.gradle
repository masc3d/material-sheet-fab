import org.deku.gradle.PackagerPlugin

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: PackagerPlugin

version '0.21-RELEASE'
sourceCompatibility = 1.8
mainClassName = "org.deku.leoz.boot.ApplicationKt"

buildscript {
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }
}

dependencies {
    compile project(':libs:sx-common')
    compile project(':libs:leoz-common')

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-runtime:$v_kotlin"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin"

    // Logging
    compile "commons-logging:commons-logging:$v_commons_logging"
    compile "ch.qos.logback:logback-classic:$v_logback"
    compile "org.slf4j:jcl-over-slf4j:$v_slf4j"

    // command line
    compile "com.beust:jcommander:1.55"

    // ini/conf file
    compile "org.ini4j:ini4j:$v_ini4j"

    // ssh support
    compile "org.apache.sshd:sshd-core:$v_sshd"

    compile "com.dooapp.fxform2:core:$v_fxform2"
    compile "org.jfxtras:jfxtras-all:8.0-r5"

    testCompile group: 'junit', name: 'junit', version: v_junit
}

sourceSets {
    // Add resources (like .fxml files) from souce path.
    // Non-standard, but nicer to keep .fxml files together with classes
    main.resources.srcDir 'src/main/java'
}

jar {
    manifest {
        attributes(
                'SplashScreen-Image': 'images/DEKU.splash.png'
        )
    }
}
tasks.classes.dependsOn(copySxPlatformDirToBuild)
tasks.classes.dependsOn(copyGlobalPlatformDirToBuild)

distributions {
    main {
        contents {
            from projectBuildSupplementalsDir
        }
    }
}

packager {
    title = "Leoz Boot"
    releaseBasePath = g_release_path
    osxIcon = g_osx_icon
    windowsIcon = g_windows_icon
    supplementalPlatformDirs(
            (projectBuildPlatformDir): (platformDirRelative)
    )
    createSelfExtractingArchive = true
}
