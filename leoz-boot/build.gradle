import java.nio.file.Paths

buildscript {
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }
    repositories {
        mavenCentral()
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: org.deku.gradle.PackagerPlugin

version '0.1'
sourceCompatibility = 1.8
mainClassName = "org.deku.leo2.boot.BootPackage"

dependencies {
    compile project(':libraries:sx-common')

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-runtime:$v_kotlin"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin"

    // Logging
    compile 'commons-logging:commons-logging:+'
    compile "ch.qos.logback:logback-classic:+"
    compile "org.slf4j:jcl-over-slf4j:+"

    compile "org.jfxtras:jfxtras-all:8.+"
    compile "com.dooapp.fxform2:core:8.+"

    compile "org.eclipse.jgit:org.eclipse.jgit:$v_jgit"

    testCompile group: 'junit', name: 'junit', version: '4.+'
}

jar {
    manifest {
        attributes (
                'Main-Class': mainClassName,
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'SplashScreen-Image': 'images/DEKU.splash.png'
        )
    }
}

packager {
    title = "LeoZ Boot"
    releaseBasePath = g_release_path.toFile()
    osxIcon = g_osx_icon
    windowsIcon = g_windows_icon
}

// Add resources (like .fxml files) from souce path.
// Non-standard, but nicer to keep .fxml files together with classes
sourceSets.main.resources.srcDir 'src/main/java'