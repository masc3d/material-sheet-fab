import java.nio.file.Paths
import org.deku.gradle.*

buildscript {
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }
    repositories {
        mavenCentral()
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: PackagerPlugin

version '0.4'
sourceCompatibility = 1.8
mainClassName = "org.deku.leoz.boot.BootPackage"

dependencies {
    compile project(':libs:sx-common')

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-runtime:$v_kotlin"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin"

    // Logging
    compile 'commons-logging:commons-logging:+'
    compile "ch.qos.logback:logback-classic:+"
    compile "org.slf4j:jcl-over-slf4j:+"

    compile "org.jfxtras:jfxtras-all:8.+"
    compile "com.dooapp.fxform2:core:8.+"

    compile "org.eclipse.jgit:org.eclipse.jgit:$v_jgit"

    testCompile group: 'junit', name: 'junit', version: '4.+'
}

jar {
    manifest {
        attributes(
                'Main-Class': mainClassName,
                'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
        )
    }
}
tasks.classes.dependsOn(copySxBinariesToBuild)

distributions {
    main {
        contents {
            from projectBuildSupplementalsDir
        }
    }
}

packager {
    title = "LeoZ Boot"
    releaseBasePath = g_release_path
    osxIcon = g_osx_icon
    windowsIcon = g_windows_icon
    supplementalPlatformDirs(
            (projectBuildBinDir): new File('bin')
    )
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'

    // Add resources (like .fxml files) from souce path.
    // Non-standard, but nicer to keep .fxml files together with classes
    main.resources.srcDir 'src/main/java'
}