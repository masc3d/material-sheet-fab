group = 'sx'
version = '0.1'

buildscript {
    ext {
        v_guava = '25.1-jre'
        v_jackson = '2.9.5'
        v_javax_persistence = '2.2'
        v_jersey = '2.27'
        v_jgit = '4.11.0.201803080745-r'
        v_jaxb = '2.3.0'
        v_jna = '4.5.1'
        v_jooq = '3.10.7'
        v_jsch = '0.1.54'
        v_jsch_proxy = '0.0.9'
        v_querydsl = '4.2.1'
        v_kotlin = '1.2.41'
        v_koptional = '1.3.0'
        v_resteasy = '4.0.0.Beta3'
        v_spring = '5.0.6.RELEASE'
        v_socketio = '1.0.0'
        v_slf4j = '1.7.25'
        v_threeten = '1.3.6'
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }

    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'project-report'

sourceCompatibility = JavaVersion.VERSION_1_10
targetCompatibility = JavaVersion.VERSION_1_10

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile(
            project(':sx-common'),

            // Kotlin
            "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin",

            "com.gojuno.koptional:koptional:$v_koptional",
            "com.gojuno.koptional:koptional-rxjava2-extensions:$v_koptional",

            "org.apache.commons:commons-lang3:3.7"
    )

    compileOnly(
            // JSR-310 / java.time substitute.
            // Declared as compileOnly as android projects may want to include `com.jakewharton.threetenabp:threetenabp:1.0.5` instead.
            "org.threeten:threetenbp:$v_threeten",

            // JAX-RS
            "javax.ws.rs:javax.ws.rs-api:2.1",
            "org.zalando:problem:0.20.3",

            // JNA
            "net.java.dev.jna:jna:$v_jna",
            "net.java.dev.jna:jna-platform:$v_jna",

            // REST
            // Jersey REST client
            "org.glassfish.jersey.core:jersey-client:$v_jersey",
            "org.glassfish.jersey.ext:jersey-proxy-client:$v_jersey",

            // RESTeasy client
            "org.jboss.resteasy:resteasy-client:$v_resteasy",
            "org.jboss.resteasy:resteasy-jackson2-provider:$v_resteasy",

            // YAML
            "org.yaml:snakeyaml:1.19",

            // Jackson
            "com.fasterxml.jackson.core:jackson-databind:$v_jackson",
            "com.fasterxml.jackson.core:jackson-annotations:$v_jackson",

            // JGit
            "org.eclipse.jgit:org.eclipse.jgit:$v_jgit",
            // For authenticating ssh/jgit against key agent
            "com.jcraft:jsch:$v_jsch",
            "com.jcraft:jsch.agentproxy.connector-factory:$v_jsch_proxy",
            "com.jcraft:jsch.agentproxy.jsch:$v_jsch_proxy",
            "com.jcraft:jsch.agentproxy.sshagent:$v_jsch_proxy",
            "com.jcraft:jsch.agentproxy.pageant:$v_jsch_proxy",

            // QueryDSL
            "com.querydsl:querydsl-core:$v_querydsl",
            "com.querydsl:querydsl-jpa:$v_querydsl",

            // Jooq
            "org.jooq:jooq:$v_jooq",

            // JPA
            "javax.persistence:javax.persistence-api:$v_javax_persistence",
            dependencies.create("org.eclipse.persistence:eclipselink:$v_eclipselink") {
                exclude module: 'javax.persistence'
            },

            // JUnit
            "junit:junit:4.12",

            // Digest utils (security)
            "commons-codec:commons-codec:1.11",

            // Spring
            "org.springframework:spring-core:$v_spring"
    )

    testCompile(
            "junit:junit:4.12",
            "ch.qos.logback:logback-classic:$v_logback",

            "org.threeten:threetenbp:$v_threeten",

            // Guava (for memory measurement etc.)
            "com.google.guava:guava:$v_guava",

            "org.jetbrains.kotlin:kotlin-reflect:$v_kotlin",

            // Jackson
            "com.fasterxml.jackson.core:jackson-databind:$v_jackson",
            "com.fasterxml.jackson.core:jackson-annotations:$v_jackson",
            "com.fasterxml.jackson.module:jackson-module-kotlin:$v_jackson"
    )
}

//region Test artifact configuration
// For sharing test classes (eg. for running as android instrumentation in android projects)
configurations {
    testArtifacts
}

task testJar(type: Jar) {
    classifier "test"
    from sourceSets.test.output
}

artifacts {
    testArtifacts testJar
}
//endregion
