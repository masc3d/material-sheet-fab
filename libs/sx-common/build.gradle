group = 'sx'
version = '0.1'

buildscript {
    ext {
        v_feign = '9.7.0'
        v_jackson = '2.9.5'
        v_jooq = '3.10.7'
        v_kotlin = '1.2.41'
        v_koptional = '1.3.0'
        v_logback = '1.2.3'
        v_requery = '3a6b32222a'
        v_socketio = '1.0.0'
        v_slf4j = '1.7.25'
        v_threeten = '1.3.6'
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin"
    }

    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'project-report'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        // masc20181101. android support
        // API levels < 24 are not compatible with some java8 bytecode kt compiler produces
        jvmTarget = JavaVersion.VERSION_1_6
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile(
            // Kotlin
            "org.jetbrains.kotlin:kotlin-stdlib:$v_kotlin",

            // Logging
            "org.slf4j:slf4j-api:$v_slf4j",
            "org.slf4j:jcl-over-slf4j:$v_slf4j",
            "org.slf4j:jul-to-slf4j:$v_slf4j",

            // RX
            "io.reactivex.rxjava2:rxjava:2.1.14",
            "io.reactivex:rxkotlin:2.0.0-RC1",

            "com.gojuno.koptional:koptional:$v_koptional",
            "com.gojuno.koptional:koptional-rxjava2-extensions:$v_koptional",

            "org.apache.commons:commons-lang3:3.7"
    )

    compileOnly(
            // JSR-310 / java.time substitute.
            // Declared as compileOnly as android projects may want to include `com.jakewharton.threetenabp:threetenabp:1.0.5` instead.
            "org.threeten:threetenbp:$v_threeten",

            // JAX-RS
            "javax.ws.rs:javax.ws.rs-api:2.1",
            "org.zalando:problem:0.20.3",

            // REST

            // Feign REST
            "io.github.openfeign:feign-jackson:$v_feign",
            "io.github.openfeign:feign-okhttp:$v_feign",
            "io.github.openfeign:feign-jaxrs2:$v_feign",

            // YAML
            "org.yaml:snakeyaml:1.19",

            // Jackson
            "com.fasterxml.jackson.core:jackson-databind:$v_jackson",
            "com.fasterxml.jackson.core:jackson-annotations:$v_jackson",

            // Requery
            "com.github.masc3d.requery:requery:$v_requery",
            "com.github.masc3d.requery:requery-kotlin:$v_requery",

            // JUnit
            "junit:junit:4.12",

            // Digest utils (security)
            "commons-codec:commons-codec:1.11",

            // Logback
            "ch.qos.logback:logback-classic:$v_logback",
    )

    testCompile(
            "junit:junit:4.12",
            "ch.qos.logback:logback-classic:$v_logback",

            "org.threeten:threetenbp:$v_threeten",

            "org.jetbrains.kotlin:kotlin-reflect:$v_kotlin",

            // Jackson
            "com.fasterxml.jackson.core:jackson-databind:$v_jackson",
            "com.fasterxml.jackson.core:jackson-annotations:$v_jackson",
            "com.fasterxml.jackson.module:jackson-module-kotlin:$v_jackson"
    )
}

//region Test artifact configuration
// For sharing test classes (eg. for running as android instrumentation in android projects)
configurations {
    testArtifacts
}

task testJar(type: Jar) {
    classifier "test"
    from sourceSets.test.output
}

artifacts {
    testArtifacts testJar
}
//endregion
