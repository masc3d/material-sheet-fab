buildscript {
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:0.13.+"
    }

    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'

repositories {
    mavenCentral()
}

dependencies {
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-runtime:0.13.+"
    compile "org.jetbrains.kotlin:kotlin-stdlib:0.13.+"

    compile "org.apache.commons:commons-lang3:3.+"
    compile "org.eclipse.jgit:org.eclipse.jgit:4.+"
    compile 'com.google.guava:guava:18.+'
    compile 'org.ini4j:ini4j:+'

    // For authenticating ssh/jgit against key agent
    compile 'com.jcraft:jsch.agentproxy.connector-factory:+'
    compile 'com.jcraft:jsch.agentproxy.jsch:+'
    compile 'com.jcraft:jsch.agentproxy.sshagent:+'
    compile 'com.jcraft:jsch.agentproxy.pageant:+'
}

def srcdir_import = file("$projectDir/src/import/java")

sourceSets {
    main.java.srcDirs += srcdir_import
    main.java.srcDirs += 'src/main/kotlin'
}

// Task for importing classes from other projects
task syncClasses(type: Sync) {
    from '../libs/leoz-common/src/main/java', '../libs/sx-common/src/main/java'
    into srcdir_import
    include '**/bundle/**', '**/rsync/**', '**/system/**', '**/ProcessExecutor*', '**/Disposable*/', '**/EmbeddedExecutable*/', '**/Action*/', '**/platform/**', '**/Bundle'
    includeEmptyDirs = false
}

// Clean build directory if class sync task performed action
tasks.syncClasses.doLast {
    new File(project.projectDir, 'build').deleteDir()
}

// Copy classes before compilation
compileJava.dependsOn(syncClasses)
compileKotlin.dependsOn(syncClasses)
